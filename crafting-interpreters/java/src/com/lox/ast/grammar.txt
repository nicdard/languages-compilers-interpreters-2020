program         → declaration* EOF ;
declaration     → classDecl
                | varDecl
                | funDecl
                | statement ;
classDecl       → "class" IDENTIFIER ( "<" IDENTIFIER )?
                  "{" ( function | "class" function )* "}" ;
funDecl         → "fun" function ;
function        → IDENTIFIER "(" parameters? ")" block ;
parameters      → IDENTIFIER ( "," IDENTIFIER )* ;
varDecl         → "var" IDENTIFIER ( "=" expression )? ";" ;
statement       → ifStmt
                | exprStmt
                | printStmt
                | whileStmt
                | block
                | breakStmt
                | returnStmt ;
returnStmt      → "return" expression? ";" ;
block           → "{" declaration* "}" ;
breakStmt       → "break" ";" ;
whileStmt       → "while" "(" expression ")" statement ;
forStmt         → "for" "(" ( varDecl | exprStmt | ";")
                            expression? ";"
                            expression? ")" statement ;
ifStmt          → "if" "(" expression ")" statement
                ( "else" statement )? ;
exprStmt        → expression ";" ;
printStmt       → "print" expression ";" ;
expression      → assignment ;
assignment      → ( call "." )? IDENTIFIER = assignment
                | ternary ;
ternary         → comma ( "?" expression ":" expression )* ;
comma           → logic_or ( "," logic_or )* ;
logic_or        → logic_and ( "or" logic_and )* ;
logic_and       → equality ( "and" equality )* ;
equality        → comparison ( ( "!=" | "==" ) comparison )* ;
comparison      → term ( ( ">" | ">=" | "<" | "<=" ) term )* ;
term            → factor ( ( "-" | "+" ) factor )* ;
factor          → unary ( ( "/" | "*" ) unary )* ;
unary           → ( "!" | "-" ) unary
                | call ;
call            → primary ( "(" arguments? ")" | "." IDENTIFIER )* ;
arguments       → expression ( "," expression )* ;
primary         → NUMBER | STRING
                | "true" | "false" | "nil"
                | "(" expression ")"
                | IDENTIFIER // Access a variable
                | funExpression
                | "super" "." IDENTIFIER;
funExpression   → "fun" "(" parameters? ")" block ;
                | assignment ;
